
// Freely inspired by: https://en.wikipedia.org/wiki/Fox,_goose_and_bag_of_beans_puzzle
//Position of each protagonist (0= left, 1=crossed)
int goat_pos = 0;
int fox_pos = 0;
int beans_pos = 0;
int farmer_pos = 0;

// Was something eaten ?
bool eaten;

// Should satisfy:
property Crosssafe = E((A X !eaten)U(goat_pos == fox_pos &  fox_pos == beans_pos & beans_pos == farmer_pos & farmer_pos == 1));

do{
// cross the river, at least one item should remain
::
   tau{= farmer_pos = 1-farmer_pos =};
   alt{
    ::tau  // crossing alone
    ::when(farmer_pos != goat_pos) tau{= goat_pos = farmer_pos =}
    ::when(farmer_pos != fox_pos) tau{= fox_pos = farmer_pos =}
    ::when(farmer_pos != beans_pos) tau{= beans_pos = farmer_pos =}
   }
// what could eat what
::when(fox_pos == goat_pos & farmer_pos != goat_pos)
   tau{= eaten = true =}
::when(goat_pos == beans_pos & farmer_pos != beans_pos)
   tau{= eaten = true =}
};
